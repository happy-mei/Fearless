package base.flows

// I could have .step with a read sink (so we know that sink operation can be parallelised) _and_ a .step with a mut sink
// which must be sequential. However, this adds implementation burden for people making flowable data structures and introduces
// new sources of runtime exceptions (i.e. accidentally calling the non .seq and it throwing because it would have to be
// unimplemented). Because _Sink/2 is a private trait, we can just manually make sure that a _Sink that would be unsafe
// to parallelise never runs in parallel. This should be easy because basically all parallel implementations of _Sink
// will be Magic!.

FlowOp[E]: {
  mut .step(sink: mut _Sink[E]): Void,
  mut .stopUp: Void,
  mut .isRunning: Bool,
  mut .for(downstream: mut _Sink[E]): Void -> Block#
    .loop {Block#
      .if {this.isRunning.not} .return {Block#(downstream.stopDown, ControlFlow.break)}
      .do {this.step(downstream)}
      .return {ControlFlow.continue}
      }
    .return {Void},
  mut .isFinite: Bool -> True,
  /// Mutates the data source for this flow to be a sub-section of the original input. This is useful for fork-join
  /// data-parallelism. Returning an empty optional either means that there is no data left to split, the
  /// data source does not think splitting further would be beneficial for parallelism, or that the data source
  /// does not wish to be run in data parallel. On the other side, implementing this method does not guarantee that
  /// this flow operator will be run in data parallel. That decision is up to the compiler and runtime.
  mut .split: mut Opt[mut FlowOp[E]] -> {},
  read .canSplit: Bool -> False,
  }


_SinkDecorator: {#[E](sink: mut _Sink[E]): mut _Sink[E]}
_Sink[T]: {
  mut #(x: T): Void,
  mut .pushError(info: Info): Void,
  mut .stopDown: Void,
  }

// IMPORTANT NOTE: REUSE SINK OBJECTS! THE IDENTITY IS IMPORTANT FOR WHEN THEY ARE RAN IN PARALLEL!!!!!

_Filter: {
  #[E](sinkDecorator: _SinkDecorator, upstream: mut FlowOp[E], predicate: read F[E, Bool]): mut FlowOp[E] -> Block#
    .let sink = {Slots#[mut _Sink[E]]}
    .return{{
      .step(downstream) ->
        upstream.step(sink.getOrFill{this.impl(downstream, predicate, sinkDecorator)}),
      .for(downstream) ->
        upstream.for(sink.getOrFill{this.impl(downstream, predicate, sinkDecorator)}),
      .stopUp -> upstream.stopUp,
      .isRunning -> upstream.isRunning,
      .split -> upstream.split.map{right -> this#(sinkDecorator, right, predicate)},
      }},
  .impl[E](downstream: mut _Sink[E], predicate: read F[E, Bool], sinkDecorator: _SinkDecorator): mut _Sink[E] -> {
    #(e) -> predicate#e ? {.then -> downstream#e, .else -> {}},
    .stopDown -> downstream.stopDown,
    .pushError(info) -> downstream.pushError(info),
    },
  }
_Map: {
  #[E,R](sinkDecorator: _SinkDecorator, upstream: mut FlowOp[E], f: read F[E, R]): mut FlowOp[R] -> Block#
    .let sink = {Slots#[mut _Sink[E]]}
    .return{{
      .step(downstream) ->
        upstream.step(sink.getOrFill{this.impl(downstream, f, sinkDecorator)}),
      .for(downstream) ->
        upstream.for(sink.getOrFill{this.impl(downstream, f, sinkDecorator)}),
      .stopUp -> upstream.stopUp,
      .isRunning -> upstream.isRunning,
      .split -> upstream.split.map{right -> this#[E,R](sinkDecorator, right, f)},
      }},
  .impl[E,R](downstream: mut _Sink[R], f: read F[E, R], sinkDecorator: _SinkDecorator): mut _Sink[E] -> {
    #(e) -> downstream#(f#e),
    .stopDown -> downstream.stopDown,
    .pushError(info) -> downstream.pushError(info),
    },

  #[C,E,R](sinkDecorator: _SinkDecorator, upstream: mut FlowOp[E], ctx: iso ToIso[C], f: read F[iso C, E, R]): mut FlowOp[R] -> Block#
    .openIso ctx' = ctx
    .let sink = {Slots#[mut _Sink[E]]}
    .return{{
      .step(downstream) ->
        upstream.step(sink.getOrFill{this.impl(downstream, ctx', f, sinkDecorator)}),
      .for(downstream) ->
        upstream.for(sink.getOrFill{this.impl(downstream, ctx', f, sinkDecorator)}),
      .stopUp -> upstream.stopUp,
      .isRunning -> upstream.isRunning,
      .split -> upstream.split.map{right -> this#[C,E,R](sinkDecorator, right, ctx'.iso, f)},
      }},
  .impl[C,E,R](downstream: mut _Sink[R], ctx: mut ToIso[C], f: read F[iso C, E, R], sinkDecorator: _SinkDecorator): mut _Sink[E] -> {
    #(e) -> downstream#(f#(ctx.iso.self, e)),
    .stopDown -> downstream.stopDown,
    .pushError(info) -> downstream.pushError(info),
    },
  }

_FlatMap: {
  #[E,R](sinkDecorator: _SinkDecorator, upstream: mut FlowOp[E], f: read F[E, mut Flow[R]]): mut FlowOp[R] -> Block#
    .let[mut Var[Bool]] isRunning = {Vars#[Bool]True}
    .let[mut Var[mut Opt[mut _Sink[E]]]] sink = {Vars#{}}
    .return{{'op
      .step(downstream) -> sink.get.match{
        .some(sink') -> upstream.step(sink'),
        .empty -> Block#(
          sink := (Opts#(sinkDecorator#[E]{
            .stopDown -> Block#(isRunning := False, downstream.stopDown),
            #(e) -> Block#
              .let nestedOp = {f#e.unwrapOp(mut _UnwrapFlowToken)}
              .return {nestedOp.for{
                #(e') -> op.isRunning ? {.then -> downstream#e', .else -> nestedOp.stopUp},
                .stopDown -> {},
                .pushError(info) -> downstream.pushError(info),
                }},
            .pushError(info) -> downstream.pushError(info),
            })),
          upstream.step(sink.get!)
          )
        },
      .for(downstream) -> sink.get.match{
        .some(sink') -> upstream.for(sink'),
        .empty -> Block#(
          sink := (Opts#(sinkDecorator#[E]{
            .stopDown -> Block#(isRunning := False, downstream.stopDown),
            #(e) -> Block#
              .let nestedOp = {f#e.unwrapOp(mut _UnwrapFlowToken)}
              .return {nestedOp.for{
                #(e') -> op.isRunning ? {.then -> downstream#e', .else -> nestedOp.stopUp},
                .stopDown -> {},
                .pushError(info) -> downstream.pushError(info),
                }},
            .pushError(info) -> downstream.pushError(info),
            })),
          upstream.for(sink.get!)
          )
        },
      .stopUp -> upstream.stopUp,
      .isRunning -> isRunning*,
      .split -> upstream.split.map{right -> this#(sinkDecorator, right, f)},
      }}
  }

// TODO: I'm still not 100% sure .with/1 is safe in parallel
_With: {
  #[A,B](sinkDecorator: _SinkDecorator, upstream: mut FlowOp[A], other: mut FlowOp[B]): mut FlowOp[mut Pair[A,B]] -> Block#
    .let[mut Var[Bool]] isRunning = {Vars#[Bool]True}
    .let[mut Var[mut Opt[mut _Sink[A]]]] sink = {Vars#{}}
    .return{{'op
      .step(downstream) -> sink.get.match{
        .some(sink') -> upstream.step(sink'),
        .empty -> Block#(
          sink := (Opts#(sinkDecorator#[A]{
            .stopDown -> Block#(isRunning := False, downstream.stopDown),
            #(e) -> Block#
              .return {other.step{
                #(e') -> op.isRunning ? {.then -> downstream#{.a -> e, .b -> e'}, .else -> other.stopUp},
                .stopDown -> op.stopUp,
                .pushError(info) -> downstream.pushError(info),
                }},
            .pushError(info) -> downstream.pushError(info),
            })),
          upstream.step(sink.get!)
          )
        },
      .for(downstream) -> sink.get.match{
        .some(sink') -> upstream.for(sink'),
        .empty -> Block#(
          sink := (Opts#(sinkDecorator#[A]{
            .stopDown -> Block#(isRunning := False, downstream.stopDown),
            #(e) -> Block#
              .return {other.step{
                #(e') -> op.isRunning ? {.then -> downstream#{.a -> e, .b -> e'}, .else -> other.stopUp},
                .stopDown -> op.stopUp,
                .pushError(info) -> downstream.pushError(info),
                }},
            .pushError(info) -> downstream.pushError(info),
            })),
          upstream.for(sink.get!)
          )
        },
      .stopUp -> upstream.stopUp,
      .isRunning -> isRunning*,
      .split -> Block#
        .if {upstream.canSplit.not} .return {{}}
        .if {other.canSplit.not} .return {{}}
        .return {Opts#(this#(sinkDecorator, upstream.split!, other.split!))},
      }}
  }
