package base.flows

// I could have .step with a read sink (so we know that sink operation can be parallelised) _and_ a .step with a mut sink
// which must be sequential. However, this adds implementation burden for people making flowable data structures and introduces
// new sources of runtime exceptions (i.e. accidentally calling the non .seq and it throwing because it would have to be
// unimplemented). Because _Sink/2 is a private trait, we can just manually make sure that a _Sink that would be unsafe
// to parallelise never runs in parallel. This should be easy because basically all parallel implementations of _Sink
// will be Magic!.

FlowOp[E]: {
  mut .step(sink: mut _Sink[E]): Void,
  mut .stop: Void,
  mut .isRunning: Bool,
  mut .forRemaining(downstream: mut _Sink[E]): Void -> Block#
    .loop {Block#
      .if {this.isRunning.not} .return {ControlFlow.break}
      .do {this.step(downstream)}
      .return {ControlFlow.continue}
      }
    .return {Void},
  mut .isFinite: Bool -> True,
  /// Mutates the data source for this flow to be a sub-section of the original input. This is useful for fork-join
  /// data-parallelism. Returning an empty optional either means that there is no data left to split, the
  /// data source does not think splitting further would be beneficial for parallelism, or that the data source
  /// does not wish to be run in data parallel. On the other side, implementing this method does not guarantee that
  /// this flow operator will be run in data parallel. That decision is up to the compiler and runtime.
  mut .split: mut Opt[mut FlowOp[E]] -> {},
  read .canSplit: Bool -> False,
  }


_Sink: {
  #[T](s: mut _Sink[T]): mut _Sink[T] -> s,
  }
_Sink[T]: {
  mut #(x: T): Void,
  mut .pushError(info: Info): Void,
  mut .stop: Void,
  }

// IMPORTANT NOTE: REUSE SINK OBJECTS! THE IDENTITY IS IMPORTANT FOR WHEN THEY ARE RAN IN PARALLEL!!!!!

_Filter: {
  #[E](sinkFactory: _Sink, upstream: mut FlowOp[E], predicate: read F[E, Bool]): mut FlowOp[E] -> Block#
//    .let[mut Var[Bool]] isRunning = {Var#[Bool]True}
    .let[mut Var[mut Opt[mut _Sink[E]]]] sink = {Var#{}}
    .return{{'op
      .step(downstream) -> sink.get.match{
        .some(sink') -> upstream.step(sink'),
        .empty -> Block#(
          sink := (Opts#(sinkFactory#[E]{
            .stop -> downstream.stop,
            #(e) -> predicate#e ? {.then -> downstream#e, .else -> {}},
            .pushError(info) -> downstream.pushError(info),
            })),
          upstream.step(sink.get!)
          )
        },
      .forRemaining(downstream) -> sink.get.match{
        .some(sink') -> upstream.forRemaining(sink'),
        .empty -> Block#(
          sink := (Opts#(sinkFactory#[E]{
            .stop -> downstream.stop,
            #(e) -> predicate#e ? {.then -> downstream#e, .else -> {}},
            .pushError(info) -> downstream.pushError(info),
            })),
          upstream.forRemaining(sink.get!)
          )
        },
      .stop -> upstream.stop,
      .isRunning -> upstream.isRunning,
      .split -> upstream.split.map{right -> this#(sinkFactory, right, predicate)},
      }}
  }
_Map: {
  #[E,R](sinkFactory: _Sink, upstream: mut FlowOp[E], f: read F[E, R]): mut FlowOp[R] -> Block#
    .let[mut Var[mut Opt[mut _Sink[E]]]] sink = {Var#{}}
    .return  {{'op
      .step(downstream) -> sink.get.match{
        .some(sink') -> upstream.step(sink'),
        .empty -> Block#(
          sink := (Opts#(sinkFactory#[E]{
            .stop -> downstream.stop,
            #(e) -> downstream#(f#e),
            .pushError(info) -> downstream.pushError(info),
            })),
          upstream.step(sink.get!)
          )
        },
      .forRemaining(downstream) -> sink.get.match{
        .some(sink') -> upstream.forRemaining(sink'),
        .empty -> Block#(
          sink := (Opts#(sinkFactory#[E]{
            .stop -> downstream.stop,
            #(e) -> downstream#(f#e),
            .pushError(info) -> downstream.pushError(info),
            })),
          upstream.forRemaining(sink.get!)
          )
        },
      .stop -> upstream.stop,
      .isRunning -> upstream.isRunning,
      .split -> upstream.split.map{right -> this#(sinkFactory, right, f)},
    }}
  }
_FlatMap: {
  #[E,R](sinkFactory: _Sink, upstream: mut FlowOp[E], f: read F[E, mut Flow[R]]): mut FlowOp[R] -> Block#
    .let[mut Var[Bool]] isRunning = {Var#[Bool]True}
    .let[mut Var[mut Opt[mut _Sink[E]]]] sink = {Var#{}}
    .return{{'op
      .step(downstream) -> sink.get.match{
        .some(sink') -> upstream.step(sink'),
        .empty -> Block#(
          sink := (Opts#(sinkFactory#[E]{
            .stop -> Block#(isRunning := False, downstream.stop),
            #(e) -> Block#
              .let nestedOp = {f#e.unwrapOp(mut _UnwrapFlowToken)}
              .return {nestedOp.forRemaining{
                #(e') -> op.isRunning ? {.then -> downstream#e', .else -> nestedOp.stop},
                .stop -> {},
                .pushError(info) -> downstream.pushError(info),
                }},
            .pushError(info) -> downstream.pushError(info),
            })),
          upstream.step(sink.get!)
          )
        },
      .forRemaining(downstream) -> sink.get.match{
        .some(sink') -> upstream.forRemaining(sink'),
        .empty -> Block#(
          sink := (Opts#(sinkFactory#[E]{
            .stop -> Block#(isRunning := False, downstream.stop),
            #(e) -> Block#
              .let nestedOp = {f#e.unwrapOp(mut _UnwrapFlowToken)}
              .return {nestedOp.forRemaining{
                #(e') -> op.isRunning ? {.then -> downstream#e', .else -> nestedOp.stop},
                .stop -> {},
                .pushError(info) -> downstream.pushError(info),
                }},
            .pushError(info) -> downstream.pushError(info),
            })),
          upstream.forRemaining(sink.get!)
          )
        },
      .stop -> upstream.stop,
      .isRunning -> isRunning*,
      .split -> upstream.split.map{right -> this#(sinkFactory, right, f)},
      }}
  }

// TODO: I'm still not 100% sure .with/1 is safe in parallel
_With: {
  #[A,B](sinkFactory: _Sink, upstream: mut FlowOp[A], other: mut FlowOp[B]): mut FlowOp[mut Pair[A,B]] -> Block#
    .let[mut Var[Bool]] isRunning = {Var#[Bool]True}
    .let[mut Var[mut Opt[mut _Sink[A]]]] sink = {Var#{}}
    .return{{'op
      .step(downstream) -> sink.get.match{
        .some(sink') -> upstream.step(sink'),
        .empty -> Block#(
          sink := (Opts#(sinkFactory#[A]{
            .stop -> Block#(isRunning := False, downstream.stop),
            #(e) -> Block#
              .return {other.step{
                #(e') -> op.isRunning ? {.then -> downstream#{.a -> e, .b -> e'}, .else -> other.stop},
                .stop -> op.stop,
                .pushError(info) -> downstream.pushError(info),
                }},
            .pushError(info) -> downstream.pushError(info),
            })),
          upstream.step(sink.get!)
          )
        },
      .forRemaining(downstream) -> sink.get.match{
        .some(sink') -> upstream.forRemaining(sink'),
        .empty -> Block#(
          sink := (Opts#(sinkFactory#[A]{
            .stop -> Block#(isRunning := False, downstream.stop),
            #(e) -> Block#
              .return {other.step{
                #(e') -> op.isRunning ? {.then -> downstream#{.a -> e, .b -> e'}, .else -> other.stop},
                .stop -> op.stop,
                .pushError(info) -> downstream.pushError(info),
                }},
            .pushError(info) -> downstream.pushError(info),
            })),
          upstream.forRemaining(sink.get!)
          )
        },
      .stop -> upstream.stop,
      .isRunning -> isRunning*,
      .split -> Block#
        .if {upstream.canSplit.not} .return {{}}
        .if {other.canSplit.not} .return {{}}
        .return {Opts#(this#(sinkFactory, upstream.split!, other.split!))},
      }}
  }
