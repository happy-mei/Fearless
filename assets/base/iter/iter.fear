package base.iter

Iter[E:*]:{
  mut .next: mut Opt[E],
  mut .find(p: mut IterPredicate[E]): mut Opt[E] -> this.next.match[mut Opt[E]]{
    .some(x) -> p#x ? { .then -> Opts#x, .else -> this.find(p) },
    .empty -> {}
    },
  mut .allMatch(p: mut IterPredicate[E]): Bool -> this.find{x -> p#x .not}.match[Bool]{
    .some(_) -> False,
    .empty -> True
    },
  mut .anyMatch(p: mut IterPredicate[E]): Bool -> this.find(p).match[Bool]{
    .some(_) -> True,
    .empty -> False
    },

  mut .map[R:*](f: mut IterMapper[E, R]): mut Iter[R] -> { this.next.map[R](f) },
  mut .flatMap[R:*](f: mut IterMapper[E, mut Iter[R]]): mut Iter[R] -> Block#
    .let[mut Iter[mut Iter[R]]] mapIter = { this.map(f) }
    .let[mut Var[mut Opt[mut Iter[R]]]] optCurIter = { Vars#(mapIter.next) }
    .return(mut ReturnStmt[mut Iter[R]]{ mut Iter[R]{ 'self
      .next -> optCurIter*[].match[mut Opt[R]]{
        .some(curIter) -> curIter.next.match[mut Opt[R]]{
          .some(next) -> Opts#next,
          .empty -> Block#(optCurIter := (mapIter.next), self.next)
          },
        .empty -> {}
        }
      }}),
  mut .filter(p: mut IterPredicate[E]): mut Iter[E] -> { .next -> this.find(p) },

  mut .fold[R:*](acc: R, f: mut IterFolder[E, R]): R -> this.next.match[R]{
    .some(e) -> this.fold(f#(acc, e), f),
    .empty -> acc
    },
  mut .for(f: mut IterForEacher[E]): Void -> this.next.match[Void]{
    .some(e) -> Block#(f#e, this.for(f)),
    .empty -> {}
    },

  mut .count: Nat -> this.fold[Nat](0, mut IterFolder[E, Nat]{ acc, _ -> acc + 1 }),
  mut .list: mut List[E] -> this.fold(List#[E], { acc, e -> Block#(acc.add(e), acc) }),
  mut .llist: mut LList[E] -> this.fold(mut LList[E], { acc, e -> acc +[] e }),
  mut .str(toStr: mut IterMapper[E, Str], joining: Str): Str -> this.map(toStr)
    .fold[Str]("", mut IterFolder[Str, Str]{acc, e -> acc.isEmpty ? {
      .then -> acc + e,
      .else -> acc + joining + e }
      }).str,
//  Flow.fromOp{'op
//    .step(sink) -> this.next.match{
//      .some(x) -> sink#x,
//      .empty -> op.stop,
//      },
//    .for(sink) -> this.map{x -> sink#x}.find{ctrl -> ctrl.match{
//      .continue -> False,
//      .break -> True,
//      .return(_) -> True,
//      }}.match{.some(ctrl) -> ctrl, .empty -> FlowCtrl.break},
//    },
  }

Sum:{
  .int(ns: mut Iter[Int]): Int -> ns.fold[Int](+0, mut IterFolder[Int, Int]{ acc, n -> acc + n }),
  .nat(ns: mut Iter[Nat]): Nat -> ns.fold[Nat](0, mut IterFolder[Nat, Nat]{ acc, n -> acc + n }),
  .float(ns: mut Iter[Float]): Float -> ns.fold[Float](0.0, mut IterFolder[Float, Float]{ acc, n -> acc + n }),
  }

IterForEacher[E:*]:{ mut #(e: E): Void }
IterFolder[E:*,R:*]:{ mut #(acc: R, e: E): R }
IterScanner[E:*, R:*, S:*]:{ mut #(state: mut Var[S], e: E): S }
IterMapper[E:*,R:*]:OptMap[E, R]{}
IterPredicate[E:*]:{ mut #(e: E): Bool }

//_ListIsoAdd[E:*]:{
//  #(l: mut List[E], e: E): mut List[E] -> Block#(l.add(e), l),
//  }
